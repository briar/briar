// Top-level build file where you can add configuration options common to all sub-projects/modules.

allprojects {
	repositories {
		mavenCentral()
		google()
		maven { url "https://jitpack.io" }
	}
	afterEvaluate {
		tasks.withType(Test) {
			// Allow tests to be re-run if any optional tests are enabled
			outputs.upToDateWhen { System.getenv("OPTIONAL_TESTS") == null }
			// Use entropy-gathering device specified on command line, if any
			systemProperty 'java.security.egd', System.getProperty('java.security.egd')
		}
	}
}

buildscript {
	repositories {
		google()
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}

	ext {
		kotlin_version = '1.9.25'
		dagger_version = "2.51.1"
		okhttp_version = "4.12.0"
		jackson_version = "2.13.4"
		tor_version = "0.4.8.21"
		lyrebird_version = "0.6.2"
		jsoup_version = '1.15.3'
		bouncy_castle_version = '1.82'
		junit_version = "4.13.2"
		jmock_version = '2.12.0'
		mockwebserver_version = '4.10.0'
		onionwrapper_version = '0.1.4'
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:8.13.0'
		classpath 'ru.vyarus:gradle-animalsniffer-plugin:2.0.1'
		classpath files('libs/gradle-witness.jar')
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

if ((project.hasProperty("briar.mailbox_integration_tests")
	&& project.property("briar.mailbox_integration_tests") == "true")
	|| System.env.MAILBOX_INTEGRATION_TESTS) {
	configure([project(':mailbox-core'), project(':mailbox-lib')]) {
		apply from: "../gradle/variables.gradle"
	}
}
